plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'dev.kikugie.stonecutter' version '0.7.10'
}

architectury {
    minecraft = project.hasProperty('minecraft_version') ? project.minecraft_version : "1.20.1"
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }
    
    // Configure common subproject
    if (project.name == 'common') {
        loom {
            accessWidenerPath = file("src/main/resources/hunger_overhaul_reborn.accesswidener")
        }
        
        architectury {
            common rootProject.enabled_platforms.split(',')
        }
        
        dependencies {
            // We depend on Fabric Loader here to use the Fabric @Environment annotations,
            // which get remapped to the correct annotations on each platform.
            // Do NOT use other classes from Fabric Loader.
            modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

            modImplementation "dev.architectury:architectury:$rootProject.architectury_api_version"
        }
    }
    
    // Apply shadow plugin to fabric and forge subprojects
    if (project.name == 'fabric' || project.name == 'forge') {
        apply plugin: 'com.github.johnrengelman.shadow'
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        mavenCentral() // For NightConfig and other standard libraries
        maven { url "https://maven.fabricmc.net/" }
        maven { url "https://maven.architectury.dev/" }
        maven { url "https://files.minecraftforge.net/maven/" }
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://jitpack.io" }
        maven { url "https://api.modrinth.com/maven" }
        maven { url "https://cursemaven.com" }
        maven { url "https://maven.glitchfiend.com/" }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}
